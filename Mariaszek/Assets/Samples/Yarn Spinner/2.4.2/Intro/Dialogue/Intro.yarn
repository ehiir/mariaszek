title: Start
---
Tom: Uh.. hello? #line:tutorial-tom-01 // tentative 
Tom: Is anyone there? #line:tutorial-tom-02
Anne: Hi there! #line:tutorial-anne-01 // chirpy 


-> Tom: Who are you? #line:tutorial-tom-03
    Tom: And where are you? #line:tutorial-tom-04 // emphasis on "where" 
    Anne: I'm Anne! I'm right here! #line:tutorial-anne-02 // trying to be helpful 
    // Set the variable $knows_anne_name to 'true'. This is the variable we
    // declared above. We'll use this value later in this file.
    //
    // If this line isn't reached (because the player chooses the other
    // dialogue option), this will stay at its default value of 'false',
    // which means the player hasn't learned Anne's name.
    <<set $knows_anne_name to true>>
-> Tom: Where am I? #line:tutorial-tom-05 
    Anne: You're floating in a featureless void! #line:tutorial-anne-03 // as though this is a perfectly reasonable explanation 
    Tom: I can see that.. #line:tutorial-tom-06 // dubious 

Tom: How did I get here? #line:tutorial-tom-07
Anne: Oh, a writer put you here. This is a test conversation for Yarn Spinner! #line:tutorial-anne-04
Tom: What's Yarn Spinner? #line:tutorial-tom-08
Anne: Don't worry about it! #line:tutorial-anne-05 // breezy 

// Check to see if the variable was set. The line that Tom says changes
// depending on whether he knows Anne's name.
<<if $knows_anne_name>>
    Tom: This is weird, Anne. #line:tutorial-tom-09
<<else>>
    Tom: This is really confusing. #line:tutorial-tom-10
<<endif>>

Anne: That's ok! This will all go away in a moment. #line:tutorial-anne-06 // reassuring 
Tom: What?! #line:tutorial-tom-11 // surprised, confused 

// The game suddenly quits. The 'quit' command is custom-defined by a
// script in this demo, in QuitGameCommand.cs.
<<quit>>

===

